plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dagger.hilt.android.plugin"
    id "com.google.devtools.ksp"
}

android {
    namespace "dev.thunderbolt.dogbreeds"
    compileSdk 34

    defaultConfig {
        applicationId "dev.thunderbolt.dogbreeds"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += [
                "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
                "-opt-in=androidx.lifecycle.viewmodel.compose.SavedStateHandleSaveableApi",
                "-opt-in=androidx.compose.foundation.layout.ExperimentalLayoutApi",
        ]
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.5.1"
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    // KOTLIN
    implementation platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version")
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0"

    // ANDROID
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "androidx.navigation:navigation-compose:2.7.2"

    // COMPOSE
    implementation platform("androidx.compose:compose-bom:$compose_version")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-graphics"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"

    // HILT
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // RETROFIT
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.11.0"

    // ROOM
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // COIL
    implementation "io.coil-kt:coil-compose:$coil_version"

    // TEST
    testImplementation "junit:junit:4.13.2"
    testImplementation "app.cash.turbine:turbine:1.0.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation platform("androidx.compose:compose-bom:$compose_version")
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
}
